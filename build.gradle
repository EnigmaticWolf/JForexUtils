buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:3.2.0'
    }
}

plugins {
    id 'net.saliman.cobertura' version '2.3.2'
    id 'com.github.kt3k.coveralls' version '2.6.3'
}
cobertura.coverageFormats = ['html', 'xml']

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'biz.aQute.bnd.builder'

group= 'com.jforex.programming'
version = '0.9.6.1'
sourceCompatibility = 1.8
targetCompatibility = 1.8

description ="""
A collection of utilities for Dukascopy's JForex API. See http://www.dukascopy.com/wiki for further documentation.
Project name: ${project.name}
"""

repositories {
   mavenCentral()
   maven {
       url "https://www.dukascopy.com/client/jforexlib/publicrepo"
   }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

dependencies {
   compile 'io.reactivex:rxjava:1.1.8'
   compile 'com.dukascopy.api:JForex-API:2.12.41'
   compile 'com.google.guava:guava:19.0'
   compile 'com.googlecode.combinatoricslib:combinatoricslib:2.1'
   compile 'org.apache.logging.log4j:log4j-api:2.5'
   compile 'org.apache.logging.log4j:log4j-core:2.5'
   compile 'org.apache.commons:commons-collections4:4.1'
   compile 'org.apache.commons:commons-lang3:3.4'
   compile 'org.aeonbits.owner:owner:1.0.9'
   compile 'com.github.oxo42:stateless4j:2.5.0'
   
   testCompile 'de.bechte.junit:junit-hierarchicalcontextrunner:4.12.1'
   testCompile 'org.hamcrest:hamcrest-all:1.3'
   testCompile 'org.mockito:mockito-all:1.10.19'
   testCompile 'info.solidsoft.mockito:mockito-java8:1.0.0-beta'
   testCompile 'junit:junit:4.12'
   testCompile 'nl.jqno.equalsverifier:equalsverifier:2.1.5'
}

gradle.projectsEvaluated {
   tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name,  
                   'Implementation-Version': version,
                   'Bundle-Name': 'JForexUtils',
                   'Bundle-SymbolicName': 'com.jforex.programming',
                   'Export-Package': 'com.jforex.programming.*'
    }
    baseName = project.name
}

task uberJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': project.name,  
                   'Implementation-Version': version,
                   'Class-Path': '.' + ' ' + configurations.compile.collect { it.getName() }.join(' ')
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task copyJarToPlugins(type: Copy, dependsOn: ':jar') {
    if (project.hasProperty('pluginFolder')) {
        from "${project.buildDir}/libs/${jar.archiveName}"
        into pluginFolder
    }
}

task copyUberJarToPlugins(type: Copy, dependsOn: ':uberJar') {
    if (project.hasProperty('pluginFolder')) {
        from "${project.buildDir}/libs/${jar.archiveName}"
        into pluginFolder
    }
}
